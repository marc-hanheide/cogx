(define (domain george-yr3)

  (:requirements :mapl :adl :fluents :action-costs :equality)

  (:types
    Robot - agent
    VisualObject ProtoObject ViewCone ConceptName - object
    ColorName ShapeName TypeName - ConceptName
  )

  (:constants
    VISUAL_OBJECT_DUMMY - VisualObject
    UNKNOWN_COLOR - ColorName
    UNKNOWN_SHAPE - ShapeName
    UNKNOWN_TYPE - TypeName
    TUTOR - agent
  )

  (:predicates
    (color-learned ?v - VisualObject)
    (shape-learned ?v - VisualObject)
    (type-learned ?v - VisualObject)

    (color-unlearned ?v - VisualObject)
    (shape-unlearned ?v - VisualObject)
    (type-unlearned ?v - VisualObject)

    (allow-polar-color-question ?v - VisualObject ?c - ColorName ?r - Robot)
    (allow-polar-shape-question ?v - VisualObject ?s - ShapeName ?r - Robot)
    (allow-polar-type-question ?v - VisualObject ?t - TypeName ?r - Robot)

    (color-gain-is-max ?v - VisualObject)
    (shape-gain-is-max ?v - VisualObject)
    (type-gain-is-max ?v - VisualObject)

    (learning-impossible)
    (unlearning-impossible)

    (curiosity-satisfied)

    (general-color-question-answered ?v - VisualObject)
    (general-shape-question-answered ?v - VisualObject)
    (general-type-question-answered ?v - VisualObject)

    (polar-color-question-answered ?v - VisualObject ?c - ColorName)
    (polar-shape-question-answered ?v - VisualObject ?s - ShapeName)
    (polar-type-question-answered ?v - VisualObject ?t - TypeName)
  )

  (:functions
    (color ?v - VisualObject) - ColorName
    (polar-question-color-threshold ?r - Robot) - number ;; If max-gain-value-color is higher than this we allow a polar question
    (max-gain-label-color ?v - VisualObject) - ColorName ;; This is the color with the highest potential gain for some object
    (max-gain-value-color ?v - VisualObject) - number ;; This is the color-gain of max-gain-label-color

    (shape ?v - VisualObject) - ShapeName
    (polar-question-shape-threshold ?r - Robot) - number
    (max-gain-label-shape ?v - VisualObject) - ShapeName
    (max-gain-value-shape ?v - VisualObject) - number

    (type ?v - VisualObject) - TypeName
    (polar-question-type-threshold ?r - Robot) - number
    (max-gain-label-type ?v - VisualObject) - TypeName
    (max-gain-value-type ?v - VisualObject) - number

    (target-object ?vc - ViewCone) - ProtoObject
    (po-is-associated-with ?p - ProtoObject) - VisualObject

    (current-viewcone ?r - Robot) - ViewCone
    (move-to-viewcone-cost ?vc1 - ViewCone ?vc2 - ViewCone) - number
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;                   INIT RULES                  ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (:init-rule set-move-to-viewcone-cost
    :parameters (?vc1 - ViewCone ?vc2 - ViewCone)
    :precondition 
      (and 
        (= (move-to-viewcone-cost ?vc1 ?vc2) UNKNOWN)
        (not (= (move-to-viewcone-cost ?vc2 ?vc1) UNKNOWN))
      )
    :effect (assign (move-to-viewcone-cost ?vc1 ?vc2) (move-to-viewcone-cost ?vc2 ?vc1))
  )

  (:init-rule set-polar-question-color-threshold
    :parameters (?r - Robot)
    :precondition (= (polar-question-color-threshold ?r) UNKNOWN)
    :effect (assign (polar-question-color-threshold ?r) 0.5)
  )

  (:init-rule set-polar-question-shape-threshold
    :parameters (?r - Robot)
    :precondition (= (polar-question-shape-threshold ?r) UNKNOWN)
    :effect (assign (polar-question-shape-threshold ?r) 0.5)
  )

  (:init-rule set-polar-question-type-threshold
    :parameters (?r - Robot)
    :precondition (= (polar-question-type-threshold ?r) UNKNOWN)
    :effect (assign (polar-question-type-threshold ?r) 0.5)
  )

  (:init-rule set-max-gain-value-color
     :parameters (?v - VisualObject)
     :precondition (= (max-gain-value-color ?v) UNKNOWN)
     :effect (assign (max-gain-value-color ?v) 0)
  )

  (:init-rule set-max-gain-value-shape
     :parameters (?v - VisualObject)
     :precondition (= (max-gain-value-shape ?v) UNKNOWN)
     :effect (assign (max-gain-value-shape ?v) 0)
  )

  (:init-rule set-max-gain-value-type
     :parameters (?v - VisualObject)
     :precondition (= (max-gain-value-type ?v) UNKNOWN)
     :effect (assign (max-gain-value-type ?v) 0)
  )
  

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;                     AXIOMS                    ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; to satisfy the curiosity is the "standard" goal, i.e. whenever George wants to learn stuff about objects

  (:derived (curiosity-satisfied)
    (and
      (forall (?p - ProtoObject) (exists (?v - VisualObject) (and (= (po-is-associated-with ?p) ?v) (color-learned ?v))))
      (unlearning-impossible)
    )
  )

;; this mirrors learning-impossible, but it doesn't work currently

  (:derived (unlearning-impossible)
    (forall (?v - VisualObject)
      (and
        (or
	  (forall (?c - ColorName) (not (neg-attributed TUTOR (color ?v) ?c)))
          (color-unlearned ?v)
        )
        (or
          (forall (?s - ShapeName) (not (neg-attributed TUTOR (shape ?v) ?s)))
          (shape-unlearned ?v)
        )
        (or
          (forall (?t - TypeName) (not (neg-attributed TUTOR (type ?v) ?t)))
          (type-unlearned ?v)
        )
      )
    )
  )

;; learning is impossible if nothing had been attributed by the TUTOR for all VisualObjects

  (:derived (learning-impossible)
    (forall (?v - VisualObject)
      (and
        (or
	  (forall (?c - ColorName) (not (attributed TUTOR (color ?v) ?c)))
          (color-learned ?v)
        )
        (or
          (forall (?s - ShapeName) (not (attributed TUTOR (shape ?v) ?s)))
          (shape-learned ?v)
        )
        (or
          (forall (?t - TypeName) (not (attributed TUTOR (type ?v) ?t)))
          (type-learned ?v)
        )
      )
    )
  )

;; A polar question is allowed for the color/shape with the max gain only,
;; and only if this gain is higher than some threshold

  (:derived (allow-polar-color-question ?v - VisualObject ?c - ColorName ?r - Robot)
    (and
      (>= (max-gain-value-color ?v) (polar-question-color-threshold ?r))
      (= (max-gain-label-color ?v) ?c)
    )
  )

  (:derived (allow-polar-shape-question ?v - VisualObject ?s - ShapeName ?r - Robot)
    (and
      (>= (max-gain-value-shape ?v) (polar-question-shape-threshold ?r))
      (= (max-gain-label-shape ?v) ?s)
    )
  )

  (:derived (allow-polar-type-question ?v - VisualObject ?t - TypeName ?r - Robot)
    (and
      (>= (max-gain-value-type ?v) (polar-question-type-threshold ?r))
      (= (max-gain-label-type ?v) ?t)
    )
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;                     ACTIONS                   ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;                MOVE HEAD                ;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (:action move-to-viewcone
    :agent (?r - Robot)
    :parameters (?vc1 - ViewCone ?vc2 - ViewCone)
    :precondition
      (and
        (= (current-viewcone ?r) ?vc1)
        (learning-impossible)
      )
    :effect
      (and
        (assign (current-viewcone ?r) ?vc2)
        (increase (total-cost) (move-to-viewcone-cost ?vc1 ?vc2))
      )
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;        ANALYZE PROTO OBJECT             ;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (:action analyze-proto-object
    :agent (?r - Robot)
    :parameters (?vc - ViewCone ?p - ProtoObject)
    :precondition
      (and
        (= (current-viewcone ?r) ?vc)
        (= (target-object ?vc) ?p)
        (not (exists (?vo - visualobject) (= (po-is-associated-with ?p) ?vo)))
        (learning-impossible)
      )
    :effect
      (and
        (assign (po-is-associated-with ?p) VISUAL_OBJECT_DUMMY)
        (increase (total-cost) 1)
      )
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;       ASK FOR OBJECT PROPERTIES         ;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (:action ask-for-an-objects-color-general
    :agent (?r - Robot)
    :parameters (?v - VisualObject)
    :precondition
      (and
        (exists (?p - ProtoObject) (= (po-is-associated-with ?p) ?v))
        (learning-impossible)
      )
    :effect 
      (and 
        (attributed TUTOR (color ?v) UNKNOWN_COLOR)
        (increase (total-cost) 2)
      )
  )

  (:action ask-for-an-objects-color-polar
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?c - ColorName)
    :precondition
      (and
        (exists (?p - ProtoObject) (= (po-is-associated-with ?p) ?v))
        (allow-polar-color-question ?v ?c ?r)
        (learning-impossible)
      )
    :effect 
      (and 
        (attributed TUTOR (color ?v) ?c)
        (increase (total-cost) 1)
      )
  )

  (:action ask-for-an-objects-shape-general
    :agent (?r - Robot)
    :parameters (?v - VisualObject)
    :precondition
      (and
        (exists (?p - ProtoObject) (= (po-is-associated-with ?p) ?v))
        (learning-impossible)
      )
    :effect 
      (and 
        (attributed TUTOR (shape ?v) UNKNOWN_SHAPE)
        (increase (total-cost) 2)
      )
  )

  (:action ask-for-an-objects-shape-polar
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?s - ShapeName)
    :precondition
      (and
        (exists (?p - ProtoObject) (= (po-is-associated-with ?p) ?v))
        (allow-polar-shape-question ?v ?s ?r)
        (learning-impossible)
      )
    :effect 
      (and 
        (attributed TUTOR (shape ?v) ?s)
        (increase (total-cost) 1)
      )
  )

  (:action ask-for-an-objects-type-general
    :agent (?r - Robot)
    :parameters (?v - VisualObject)
    :precondition
      (and
        (exists (?p - ProtoObject) (= (po-is-associated-with ?p) ?v))
        (learning-impossible)
      )
    :effect 
      (and 
        (attributed TUTOR (type ?v) UNKNOWN_TYPE)
        (increase (total-cost) 2)
      )
  )

  (:action ask-for-an-objects-type-polar
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?t - TypeName)
    :precondition
      (and
        (exists (?p - ProtoObject) (= (po-is-associated-with ?p) ?v))
        (allow-polar-type-question ?v ?t ?r)
        (learning-impossible)
      )
    :effect 
      (and 
        (attributed TUTOR (type ?v) ?t)
        (increase (total-cost) 1)
      )
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;                   LEARNING              ;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (:action learn-color
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?c - ColorName)
    :precondition
      (and
	(not (color-learned ?v))
        (attributed TUTOR (color ?v) ?c)
        (exists (?p - ProtoObject) 
          (and 
            (= (po-is-associated-with ?p) ?v)
            (= (target-object (current-viewcone ?r)) ?p)
          )
        )
      )
    :effect 
      (and
	(color-learned ?v)
	(increase (total-cost) 1)
      )
  )

  (:action learn-shape
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?s - ShapeName)
    :precondition 
      (and
	(not (shape-learned ?v))
        (attributed TUTOR (shape ?v) ?s)
        (exists (?p - ProtoObject) 
          (and 
            (= (po-is-associated-with ?p) ?v)
            (= (target-object (current-viewcone ?r)) ?p)
          )
        )
      )
    :effect 
      (and
	(shape-learned ?v)
	(increase (total-cost) 1)
      )
  )

  (:action learn-type
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?t - TypeName)
    :precondition 
      (and
	(not (type-learned ?v))
        (attributed TUTOR (type ?v) ?t)
        (exists (?p - ProtoObject) 
          (and 
            (= (po-is-associated-with ?p) ?v)
            (= (target-object (current-viewcone ?r)) ?p)
          )
        )
      )
    :effect 
      (and
	(type-learned ?v)
	(increase (total-cost) 1)
      )
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;                  UNLEARNING             ;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (:action unlearn-color
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?c - ColorName)
    :precondition
      (and
	(not (color-unlearned ?v))
        (neg-attributed TUTOR (color ?v) ?c)
        (exists (?p - ProtoObject) 
          (and 
            (= (po-is-associated-with ?p) ?v)
            (= (target-object (current-viewcone ?r)) ?p)
          )
        )
      )
    :effect 
      (and
	(color-unlearned ?v)
	(increase (total-cost) 1)
      )
  )

  (:action unlearn-shape
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?s - ShapeName)
    :precondition 
      (and
	(not (shape-unlearned ?v))
        (neg-attributed TUTOR (shape ?v) ?s)
        (exists (?p - ProtoObject) 
          (and 
            (= (po-is-associated-with ?p) ?v)
            (= (target-object (current-viewcone ?r)) ?p)
          )
        )
      )
    :effect 
      (and
	(shape-unlearned ?v)
	(increase (total-cost) 1)
      )
  )

  (:action unlearn-type
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?t - TypeName)
    :precondition 
      (and
	(not (type-unlearned ?v))
        (neg-attributed TUTOR (type ?v) ?t)
        (exists (?p - ProtoObject) 
          (and 
            (= (po-is-associated-with ?p) ?v)
            (= (target-object (current-viewcone ?r)) ?p)
          )
        )
      )
    :effect 
      (and
	(type-unlearned ?v)
	(increase (total-cost) 1)
      )
  )

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;          QUESTION ANSWERING             ;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

  (:action answer-general-color-question
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?c - ColorName)
    :precondition 
      (and
	(attributed TUTOR (color ?v) ?c)
      )
    :effect 
      (and  
        (general-color-question-answered ?v)
	(increase (total-cost) 1)
      )
  )

  (:action answer-general-shape-question
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?s - ShapeName)
    :precondition 
      (and
	(attributed TUTOR (shape ?v) ?s)
      )
    :effect 
      (and  
        (general-shape-question-answered ?v)
	(increase (total-cost) 1)
      )
  )

  (:action answer-general-type-question
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?t - TypeName)
    :precondition 
      (and
	(attributed TUTOR (type ?v) ?t)
      )
    :effect 
      (and  
        (general-type-question-answered ?v)
	(increase (total-cost) 1)
      )
  )

  (:action answer-polar-color-question-positively
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?c - ColorName)
    :precondition 
      (and
	(attributed TUTOR (color ?v) ?c)
      )
    :effect 
      (and  
        (polar-color-question-answered ?v ?c)
	(increase (total-cost) 1)
      )
  )

  (:action answer-polar-color-question-negatively
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?c - ColorName)
    :precondition 
      (and
	(not (attributed TUTOR (color ?v) ?c))
        (exists 
          (?c2 - ColorName)
          (and 
            (attributed TUTOR (color ?v) ?c2)
            (not (= ?c2 UNKNOWN_COLOR))
          )
        )
      )
    :effect 
      (and  
        (polar-color-question-answered ?v ?c)
	(increase (total-cost) 1)
      )
  )

  (:action answer-polar-shape-question-positively
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?s - ShapeName)
    :precondition 
      (and
        (attributed TUTOR (shape ?v) ?s)
      )
    :effect 
      (and  
        (polar-shape-question-answered ?v ?s)
        (increase (total-cost) 1)
      )
  )

  (:action answer-polar-shape-question-negatively
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?s - ShapeName)
    :precondition 
      (and
        (not (attributed TUTOR (shape ?v) ?s))
        (exists 
          (?s2 - ShapeName)
          (and 
            (attributed TUTOR (shape ?v) ?s2)
            (not (= ?s2 UNKNOWN_SHAPE))
          )
        )
      )
    :effect 
      (and  
        (polar-shape-question-answered ?v ?s)
        (increase (total-cost) 1)
      )
  )

  (:action answer-polar-type-question-positively
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?t - TypeName)
    :precondition 
      (and
        (attributed TUTOR (type ?v) ?t)
      )
    :effect 
      (and  
        (polar-type-question-answered ?v ?t)
        (increase (total-cost) 1)
      )
  )

  (:action answer-polar-type-question-negatively
    :agent (?r - Robot)
    :parameters (?v - VisualObject ?t - TypeName)
    :precondition 
      (and
        (not (attributed TUTOR (type ?v) ?t))
        (exists 
          (?t2 - TypeName)
          (and 
            (attributed TUTOR (type ?v) ?t2)
            (not (= ?t2 UNKNOWN_TYPE))
          )
        )
      )
    :effect 
      (and  
        (polar-type-question-answered ?v ?t)
        (increase (total-cost) 1)
      )
  )

  (:action answer-color-question-impossible
    :agent (?r - Robot)
    :parameters (?v - VisualObject)
    :precondition
      (and
        (not (exists (?c - ColorName) (attributed TUTOR (color ?v) ?c)))
      )
    :effect
      (and
        (general-color-question-answered ?v)
        (forall (?c - ColorName) (polar-color-question-answered ?v ?c))
	(increase (total-cost) 1)
      )
  )

  (:action answer-shape-question-impossible
    :agent (?r - Robot)
    :parameters (?v - VisualObject)
    :precondition
      (and
        (not (exists (?s - ShapeName) (attributed TUTOR (shape ?v) ?s)))
      )
    :effect
      (and
        (general-shape-question-answered ?v)
        (forall (?s - ShapeName) (polar-shape-question-answered ?v ?s))
	(increase (total-cost) 1)
      )
  )

  (:action answer-type-question-impossible
    :agent (?r - Robot)
    :parameters (?v - VisualObject)
    :precondition
      (and
        (not (exists (?t - TypeName) (attributed TUTOR (type ?v) ?t)))
      )
    :effect
      (and
        (general-type-question-answered ?v)
        (forall (?t - TypeName) (polar-type-question-answered ?v ?t))
	(increase (total-cost) 1)
      )
  )


)
