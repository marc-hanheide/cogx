(define (domain george-yr3)

  (:requirements :mapl :adl :fluents :action-costs :equality)

  (:types
    Robot - agent
    VisualObject ProtoObject ViewCone ConceptName - object
    ColorName ShapeName TypeName - ConceptName
  )

  (:constants
    VISUAL_OBJECT_DUMMY - VisualObject
    UNKNOWN_COLOR - ColorName
    UNKNOWN_SHAPE - ShapeName
    UNKNOWN_TYPE - TypeName
    TUTOR - agent
  )

  (:predicates
    (color-learned ?p - ProtoObject)
    (shape-learned ?p - ProtoObject)
    (type-learned ?p - ProtoObject)
  )

  (:functions
    (color ?p - ProtoObject) - ColorName
    (shape ?v - ProtoObject) - ShapeName
    (type ?v - ProtoObject) - TypeName
    (target-object ?vc - ViewCone) - ProtoObject
    (po-is-associated-with ?p - ProtoObject) - VisualObject
    (current-viewcone ?r - Robot) - ViewCone
    (move-to-viewcone-cost ?vc1 - ViewCone ?vc2 - ViewCone) - number
  )

  (:init-rule set-move-to-viewcone-cost
     :parameters (?vc1 - ViewCone ?vc2 - ViewCone)
     :precondition 
       (and 
         (= (move-to-viewcone-cost ?vc1 ?vc2) UNKNOWN)
         (not (= (move-to-viewcone-cost ?vc2 ?vc1) UNKNOWN))
       )
     :effect (assign (move-to-viewcone-cost ?vc1 ?vc2) (move-to-viewcone-cost ?vc2 ?vc1))
  )

  (:action move-to-viewcone
    :agent (?r - Robot)
    :parameters (?vc1 - ViewCone ?vc2 - ViewCone)
    :precondition
      (and
        (= (current-viewcone ?r) ?vc1)
      )
    :effect
      (and
        (assign (current-viewcone ?r) ?vc2)
        (increase (total-cost) (move-to-viewcone-cost ?vc1 ?vc2))
      )
  )

  (:action analyze-proto-object
    :agent (?r - Robot)
    :parameters (?vc - ViewCone ?po - ProtoObject)
    :precondition
      (and
        (= (current-viewcone ?r) ?vc)
        (= (target-object ?vc) ?po)
        (not (exists (?vo - visualobject) (= (po-is-associated-with ?po) ?vo)))
      )
    :effect
      (and
        (assign (po-is-associated-with ?po) VISUAL_OBJECT_DUMMY)
        (increase (total-cost) 1)
      )
  )

  (:action ask-for-an-objects-color-general
    :agent (?r - Robot)
    :parameters (?p - ProtoObject)
    :precondition
      (and
        (exists (?vo - VisualObject) (= (po-is-associated-with ?p) ?vo))
      )
    :effect 
      (and 
        (attributed TUTOR (color ?p) UNKNOWN_COLOR)
        (increase (total-cost) 2)
      )
  )

  (:action ask-for-an-objects-shape-general
    :agent (?r - Robot)
    :parameters (?p - ProtoObject)
    :precondition
      (and
        (exists (?vo - VisualObject) (= (po-is-associated-with ?p) ?vo))
      )
    :effect 
      (and 
        (attributed TUTOR (shape ?p) UNKNOWN_SHAPE)
        (increase (total-cost) 2)
      )
  )

  (:action ask-for-an-objects-type-general
    :agent (?r - Robot)
    :parameters (?p - ProtoObject)
    :precondition
      (and
        (exists (?vo - VisualObject) (= (po-is-associated-with ?p) ?vo))
      )
    :effect 
      (and 
        (attributed TUTOR (type ?p) UNKNOWN_TYPE)
        (increase (total-cost) 2)
      )
  )

  (:action learn-color
    :agent (?r - Robot)
    :parameters (?vc - ViewCone ?p - ProtoObject ?c - ColorName)
    :precondition 
      (and
        (exists (?vo - VisualObject) (= (po-is-associated-with ?p) ?vo))
	(not (color-learned ?p))
        (attributed TUTOR (color ?p) ?c)
        (= (current-viewcone ?r) ?vc)
        (= (target-object ?vc) ?p)
      )
    :effect 
      (and
	(color-learned ?p)
	(increase (total-cost) 1)
      )
  )

  (:action learn-shape
    :agent (?r - Robot)
    :parameters (?vc - ViewCone ?p - ProtoObject ?s - ShapeName)
    :precondition 
      (and
        (exists (?vo - VisualObject) (= (po-is-associated-with ?p) ?vo))
	(not (shape-learned ?p))
        (attributed TUTOR (shape ?p) ?s)
        (= (current-viewcone ?r) ?vc)
        (= (target-object ?vc) ?p)
      )
    :effect 
      (and
	(shape-learned ?p)
	(increase (total-cost) 1)
      )
  )

  (:action learn-type
    :agent (?r - Robot)
    :parameters (?vc - ViewCone ?p - ProtoObject ?t - TypeName)
    :precondition 
      (and
        (exists (?vo - VisualObject) (= (po-is-associated-with ?p) ?vo))
	(not (type-learned ?p))
        (attributed TUTOR (type ?p) ?t)
        (= (current-viewcone ?r) ?vc)
        (= (target-object ?vc) ?p)
      )
    :effect 
      (and
	(type-learned ?p)
	(increase (total-cost) 1)
      )
  )
)
